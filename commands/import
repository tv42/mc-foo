#!/usr/bin/perl -w
use strict;
use vars qw($BACKEND $PATH $VOLUME
           $MCFOOHOME $MCFOODATA $MCFOOCACHE);

$MCFOOHOME=$ENV{MCFOOHOME} || '/usr/lib/mc-foo';
$MCFOODATA=$ENV{MCFOODATA} || '/var/lib/mc-foo';
$MCFOOCACHE=$ENV{MCFOOCACHE} || '/var/cache/mc-foo';

my $USAGE = "import <backend> <path> [volume]";
sub usage() {
  die "usage: \n$USAGE\n";
}
sub fail(@) { die "$0: @_\n"; }

sub munge($;) {
  my ($a) = @_;
  $a=~tr{a-zA-Z0-9}{_}c;
  return $a;
}

if (@ARGV) {
  if ($ARGV[0] eq 'help') {
    print "import - import songs to mc-foo media database\n";
    exit 0;
  }
  if ($ARGV[0] eq 'usage') {
    print "$USAGE\n";
    exit 0;
  }
}

usage() if @ARGV < 2 or @ARGV > 3;
$BACKEND=$ARGV[0];
$PATH=$ARGV[1];
$VOLUME=$ARGV[2];

$PATH =~ m{^/} or fail "path $PATH must be an absolute path.";
-d $PATH or fail "path $PATH not found: $!";

-d "$MCFOODATA/media/$BACKEND"
  or fail "path $MCFOODATA/media/$BACKEND not found: $!";
my $mpath = munge $PATH;

if (not -d "$MCFOODATA/media/$BACKEND/$mpath") {
  mkdir "$MCFOODATA/media/$BACKEND/$mpath", 0755
    or fail "cannot create dir $MCFOODATA/media/$BACKEND/$mpath: $!";
  symlink($PATH, "$MCFOODATA/media/$BACKEND/$mpath/path")
    or fail "cannot create symlink $MCFOODATA/media/$BACKEND/$mpath/path: $!";
}

if (defined $VOLUME) {
  -d "$MCFOODATA/media/$BACKEND/$mpath/removable"
    or mkdir "$MCFOODATA/media/$BACKEND/$mpath/removable", 0755
    or fail "cannot create dir $MCFOODATA/media/$BACKEND/$mpath/removable: $!";
  my $mvol = munge $VOLUME;
  
  system("make_vinyl",
         "$MCFOODATA/media/$BACKEND/$mpath/removable/$mvol.cdb",
         $PATH) == 0
           or fail "executing make_vinyl failed: $?";
  
  symlink("removable/$mvol.cdb", "$MCFOODATA/media/$BACKEND/$mpath/current.$$.tmp");
  rename("$MCFOODATA/media/$BACKEND/$mpath/current.$$.tmp",
         "$MCFOODATA/media/$BACKEND/$mpath/current",);
} else {
  system("make_vinyl",
         "$MCFOODATA/media/$BACKEND/$mpath/current",
         $PATH) == 0
           or fail "executing make_vinyl failed: $?";
}
system("$MCFOOHOME/lib/update-cache") == 0
           or fail "executing update-cache failed: $?";
exit(0);
