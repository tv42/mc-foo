#!/usr/bin/perl -w
use strict;
use CDB_File;

my $DEFAULT_SCORE=100; # everything starts with 100 points.

sub fail(@) {
  die "profiles-to-cache: @_\n";
}

sub dir(&$) {
        my ($grep) = shift;
        return () unless defined $grep;
        my ($dir) = shift;
        return () unless defined $dir;
        opendir DIR, $dir or warn("Cannot open dir $dir"), return ();
        my (@r) = sort grep {!/^\./} readdir DIR;
        closedir DIR;
        return grep { &$grep } @r;
}

sub get_profiles() {
  my @d=dir {/\.cdb$/} 'profiles';
  return map {s/\.cdb$//;$_} @d;
}

sub get_medias() {
  my @backends=dir {1} 'media';
  my @medias;
  foreach my $backend (@backends) {
    push @medias, map {"$backend/$_"}
      dir {1} "media/$backend"; #TODO filter only ones with current etc
  }
  return @medias;
}

sub open_all_profiles() {
  my %p;
  foreach (get_profiles()) {
    my %h;
    tie %h, 'CDB_File', "profiles/$_.cdb"
      or fail "unable to open profiles/$_.cdb: $!";
    $p{$_}=\%h;
  }
  return \%p;
}

my $profiles = open_all_profiles();
my %weights;

foreach my $medianame (get_medias()) {
  my %media;
  tie %media, 'CDB_File', "media/$medianame/current"
    or fail "cannot open media/$medianame/current: $!";

  my %mediaprofile;

  foreach my $songnumber (1..$media{n}) {
    my $path=$media{$songnumber};
    my %info;
    foreach (qw(title artist album year genre category)) {
      $info{$_} = $media{"$_:$path"};
      defined $info{$_} or delete $info{$_};      
    }
    foreach my $profile (keys %$profiles) {
      $weights{$profile}->{$medianame}+=$DEFAULT_SCORE;
      my $score=0;

      foreach (keys %info) {
        my $s=$profiles->{$profile}->{"score:$_:".$info{$_}};
        $score+=$s if defined $s;
      }

      if ($score != 0) {
        $mediaprofile{"$profile:$songnumber"} = $score;
        $weights{$profile}->{$medianame} += $score;
      }
    }
  }
  # write mediaprofile
  $mediaprofile{n} = $media{n};
  CDB_File::create(%mediaprofile, 
                   "cache/mediaprofiles/$medianame.cdb", 
                   "cache/mediaprofiles/$medianame.tmp.$$")
    or die; #TODO
}
# write weights
foreach (keys %weights) {
  CDB_File::create(%{$weights{$_}},
                   "cache/weights/$_.cdb", 
                   "cache/weights/$_.tmp.$$")
    or die; #TODO
}
