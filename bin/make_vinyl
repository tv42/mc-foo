#!/usr/bin/perl -w
use File::Find;
use MPEG::MP3Info;
use CDB_File;

sub usage() { die "usage: make_vinyl <cdbfile> <filetree>\n" }
sub fail(@) { die "make_vinyl: @_\n"; }

usage() if @ARGV != 2;
$CDB=$ARGV[0];
$FILES=$ARGV[1];
usage() unless not -e $CDB or -f _;
usage() unless -d $FILES;

sub munge($;) {
  my ($a) = @_;
  $a=~tr{a-zA-Z0-9}{_}c;
  return $a;
}

my $n=0;
my $h=new CDB_File $CDB, "$CDB.$$.tmp"
  or fail "cannot open cdb file $CDB: $!";
$FILES .= '/' unless $FILES=~m{ /$ }x;
my %fields = ();

sub wanted {
  if (-f $_ and -s _) {
    my $a=$File::Find::name;
    substr($a, 0, length($FILES)) = ''
      if substr($a, 0, length($FILES)) eq $FILES;

    if (/\.mp3$/i) {
      my $tag = get_mp3tag($_);
      if (defined $tag) {
	foreach my $key (keys %$tag) {
	  if (defined $tag->{$key} and $tag->{$key} ne '') {
	    my $k = 'id3-'.munge lc($key);
	    $fields{$k}=1;
	    $h->insert("$k:$a", $tag->{$key});
	  }
	}
      }
    } elsif (/\.ogg$/i) {
      #TODO
    } else {
      return;
    }

    $n++;
    $h->insert($n, $a);
  }
}

$File::Find::dont_use_nlink=1;
find(\&wanted, $FILES);

$h->insert('n', $n);
$h->insert('fields', join ' ', keys %fields);
$h->finish
  or fail "cannot finish cdb: $!";
